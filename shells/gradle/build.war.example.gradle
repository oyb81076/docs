// this is a demo for war

apply plugin: "jetty"
apply plugin: "java"
apply plugin: "idea"
apply plugin: "war"

version = '2.0.0'

sourceCompatibility = 1.7

ext {
	spring = "4.1.6.RELEASE"
	hibernateValidator = "5.1.3.Final"
	hibernate = "4.3.8.Final"
	mysql = "5.1.6"
	fastjson = "1.1.36"
}

repositories {
	mavenLocal()
	maven { url 'http://maven.oschina.net/content/groups/public/' }
}

dependencies {
	compile "org.hibernate:hibernate-core:$hibernate"
	compile "org.hibernate:hibernate-c3p0:$hibernate"

	compile "commons-beanutils:commons-beanutils:20030211.134440"

	compile "org.hibernate:hibernate-validator:$hibernateValidator"

	compile "org.springframework:spring-core:$spring"
	compile "org.springframework:spring-webmvc:$spring"
	compile "org.springframework:spring-context:$spring"
	compile "org.springframework:spring-aop:$spring"
	compile "org.springframework:spring-tx:$spring"
	compile "org.springframework:spring-orm:$spring"

	compile "taglibs:standard:1.1.2"
	compile "jstl:jstl:1.1.2"

	compile "com.alibaba:fastjson:$fastjson"

	compile "commons-io:commons-io:1.4"
	compile "commons-fileupload:commons-fileupload:1.2.2"

	compile "org.slf4j:slf4j-log4j12:1.6.0"
	compile "mysql:mysql-connector-java:$mysql"
	compile "com.google.code.maven-play-plugin.org.playframework:jj-simplecaptcha:1.1"
	compile "net.coobird:thumbnailator:0.4.6"
	
	providedCompile "org.apache.tomcat:tomcat-jsp-api:7.0.47"
}

war {
	manifest {
		attributes "Project-Version": version
		attributes "Build-Timestamp": new Date()
	}
}

task release(type:Copy) {
	def copyDetails = []
	dependsOn 'war'
	from("src/main/webapp")
	from('build/tmp/war') {
		into "META-INF"
	}
	from(configurations.runtime) {
		into "WEB-INF/lib"
	}
	from("build/classes/main") { 
		into "WEB-INF/classes"
	}
	from("build/resources/main") { 
		into "WEB-INF/classes"
	}
	eachFile { copyDetails << it }
	into "build/dist"
	doLast {
		copyDetails.each { FileCopyDetails details ->
			def target = new File(destinationDir, details.path)
			if(target.exists()) {
				target.setLastModified(details.lastModified)
			}
		}
	}
}

jettyRun {
	webAppSourceDirectory file("$projectDir/src/main/webapp")
	httpPort 8080
	contextPath ""
	scanIntervalSeconds 0
	reload "automatic"
}
